# Exceptions

1. Что такое исключение?
2. Стандартные исключения
3. Синтаксис
4. Пользовательские исключения
5. RAISE_APPLICATION_ERROR

Цель применения исключений в ORACLE - выдача дружественных к пользователю сообщений об ошибках.

**Исключение (исключительная ситуация)** - это такая ситуация, которая возникает или активизируется в ходе выполнения программы, когда дальнейшее выполнение программы не имеет смысла. Исключительная ситуация это не всегда ошибка с точки ORACLE. Часто это ошибка только с точки зрения информационной системы.

Реакция программы на возникновение исключительной ситуации описывается в обработчиках исключительных ситуация в секции исключений (exception section). Если при возникновении исключительной ситуации в секции исключений не находится соответствующего обработчика, то ORACLE обрабатывает его "своими силами".

Секция исключений, предназначенная для обработки исключений, - это идеальное место для выдачи информативных сообщений об ошибках и выполнения _очистки_ (cleanup), позволяющей избавиться от всего, что могло бы в дальнейшем вызвать путаницу или проблемы. Если исключение было активизировано в ходе выполнения процедуры, вставляющей строки в таблицу, то типичная процедура очистки может включать в себя оператор ROLLBACK.

После того, как управление было передано обработчику исключения, оно уже не возвращается оператору, ставшему причиной этого исключения, то есть работа секции выполнения прекращается.

Исключение может активизироваться либо системой, либо самим пользователем. В первом случае исключения называют _системными_, во втором - _пользовательскими._

## СИСТЕМНЫЕ ИСКЛЮЧЕНИЯ

Существуют стандартные проблемы и, соответственно, стандартные исключения. Несколько из них привидены в таблице:

| **Системное исключение** | **Причина активизации**                                                                                 |
| ------------------------ | ------------------------------------------------------------------------------------------------------- |
| CURSOR_ALREADY_OPEN      | Попытка открыть уже открытый курсор                                                                     |
| INVALID_CURSOR           | Попытка применить команду FETCH к неоткрытому курсору или попытка закрыть курсор, который не открывался |
| NO_DATA_FOUND            | Попытка выполнить SELECT INTO, когда SELECT возвращает нулевое колическто строк                         |
| TOO_MANY_ROWS            | Попытка вставить в переменную результат SELECT, который вернул больше одной строки                      |
| ZERO_DIVIDE              | Попытка деления на ноль                                                                                 |
| OTHERS                   | Все остальные исключения (используется для обработки любых исключений)                                  |

Обработчики исключений пишутся в конце базового блока (перед END) в секции исключений, которая начинается со служебного слова EXEPTION. Для обработки конкретного исключения указываются служебные слова WHEN, после которого идет название исключения, и слово THEN, после которого идет код обработчика указанного исключения. Если обработчиков исключений несколько, то для каждого из них используется такая же конструкция:

```
EXCEPTION
WHEN имя_исключения_1 THEN
операторы_обрабатывающие_исключение имя_исключения_1
[WHEN имя_исключения_2 THEN
операторы_обрабатывающие_исключение имя_исключения_2
WHEN ...
]
```

**Пример** **1:**

```
DECLARE
 a number:=6;
 b number:=0;
begin
 a:=a/b;
 b:=7;
 dbms_output.put_line('b='||b);
 EXCEPTION
 WHEN ZERO_DIVIDE THEN
	 dbms_output.put_line('Не  удалось  выполнить  операцию  деления');
	 dbms_output.put_line('a='||a||', b='||b);
END;
```

При обработке исключений можно использовать две специальные переменные - SQLCODE и SQLERRM. Первая содержит код ошибки, вторая - имя и описание ошибки.

**Пример 2:**

```
DECLARE
 a number:=6;
 b number:=0;
begin
 a:=a/b;
 b:=7;
 dbms_output.put_line('b='||b);
 EXCEPTION
 WHEN ZERO_DIVIDE THEN
 DECLARE
	 enum number:=SQLCODE;
	 emsg varchar2(512):=SQLERRM;
 BEGIN
	 dbms_output.put_line('ORA Error NUM: '||enum);
	 dbms_output.put_line('ORA Error MSG: '||emsg);
	 dbms_output.put_line('a='||a);
	 dbms_output.put_line('b='||b);
 END;
END;
```

## ПОЛЬЗОВАТЕЛЬСКИЕ ИСКЛЮЧЕНИЯ

Пользователь может создавать и собственные исключения, которые система не обнаруживает и не считает за ошибки. Например, попытка вставить в поле КОЛИЧЕСТВО отрицательную величину.

Исключения, определяемые пользователем описываются аналогично переменным в секции объявления, но только с типом EXCEPTION.

Вызываются, или активизируются, исключения оператором RAISE, после которого идет название исключения.

**Пример 3:**

```
DECLARE
 quantity_must_positive EXCEPTION;
BEGIN
...
IF (quantity<0) then
 RAISE quantity_must_positive;
END IF;
...
EXCEPTION
 WHEN quantity_must_positive THEN
  dbms_output.put_line('Количество не может быть отрицательной величиной');
...
END;
```

## Оператор RAISE_APPLICATION_ERROR

Собственное исключение можно активировать также, используя команду RAISE_APPLICATION_ERROR(ERNUM,ERMESSAGE), где ERNUM - номер ошибки (от -20000 до -20999), а ERMESSAGE - текст сообщения об ошибке. Данный способ более предпочтителен, чем вывод сообщения с помощью dbms_output.put_line, если сообщение об ошибке нужно вернуть в вызвавшее процедуру или функцию внешнее приложение, написанное на языке высокого уровня.

Применение dbms_output.put_line резонно лишь в том случае, когда функция или процедура вызывается из среды SQL Plus, то есть для отладочных целей во время разработки. Во время же эксплуатации готовой системы все сообщения, выводящиеся на экран посредством dbms_output.put_line просто не дойдут до пользователя.

** Пример 4:**

```
DECLARE
 quantity_must_positive EXCEPTION;
BEGIN
...
IF (qiantity<0) then
  RAISE_APPLICATION_ERROR(-20001,'Количество не может быть отрицательной величиной');
END IF;
...
END;
```

В этом случае, поскольку обработчика исключительной ситуации нет, управление никуда передаваться не будет, просто завершится выполение базового блока, в котором произошло исключение. В приложении, которое вызвало процедуру или функцию, содержащие данный базовый блок, можно обработать это исключение.
